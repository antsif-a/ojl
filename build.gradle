apply plugin: 'java'

group 'io.summetdev.ojl'
version '1.0'

repositories {
    google()
    mavenCentral()
    mavenLocal()
}

sourceCompatibility = 16
targetCompatibility = 16

compileJava {
    options.deprecation = true
}

ext {
    versions = [:]
    versions.lwjgl = '3.2.3'
    versions.junit = '4.11'
    versions.immutables = '2.8.2'
    versions.dagger = '2.38'

    lwjgl = [:]
    lwjgl.libs = [
        "org.lwjgl:lwjgl:$versions.lwjgl",
        "org.lwjgl:lwjgl-glfw:$versions.lwjgl",
        "org.lwjgl:lwjgl-opengl:$versions.lwjgl",
        "org.lwjgl:lwjgl-stb:$versions.lwjgl"
    ]
    lwjgl.platforms = ['windows', 'linux', 'macos']
}

dependencies {
    // lwjgl
    lwjgl.libs.each { lib ->
        implementation lib

        lwjgl.platforms.each { platform ->
            runtimeOnly "$lib:natives-$platform"
        }
    }

    // immutables
    compileOnly "org.immutables:value:$versions.immutables"
    annotationProcessor "org.immutables:value:$versions.immutables"

    // di
    implementation "com.google.dagger:dagger:$versions.dagger"
    annotationProcessor "com.google.dagger:dagger-compiler:$versions.dagger"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    annotationProcessor 'javax.annotation:javax.annotation-api:1.3.2'

    // tests
    testImplementation "junit:junit:$versions.junit"
    testImplementation "com.google.dagger:dagger:$versions.dagger"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:$versions.dagger"
    testImplementation 'javax.annotation:javax.annotation-api:1.3.2'
    testAnnotationProcessor 'javax.annotation:javax.annotation-api:1.3.2'
}
